Dans LibreOffice Writer

Aller dans :

-> Outils
   -> Macros
      -> Gerer les macros
         -> Basic...

Puis choisir :

-> Editer
   Copier le texte qui est sous les "--------" dans la grande fenetre haut/droite 

Modifier les lignes les plus indentées comme vous le souhaitez.

Puis :

-> Executer
     




------------------------------------------------------------------------------------------

Sub ChangeFormulasSizeAndFont()
    '''Changer la taille ou la police de toutes les formules d'un document'''
    Dim embeddedObjects As Object ' com.sun.star.text.TextEmbeddedObjects
    Dim elementNames() As String ' Name array
    Dim element As Object ' com.sun.star.text.TextEmbeddedObject.Model
    Dim realElem As Object ' com.sun.star.text.TextEmbeddedObject.(get)EmbeddedObject

    embeddedObjects = ThisComponent.getEmbeddedObjects() ' com.sun.star.text.TextEmbeddedObjects
    elementNames = embeddedObjects.getElementNames() ' name array

    For i = 0 To UBound(elementNames)

        element = embeddedObjects.getByName(elementNames(i)).Model
        If (not isNull(element)) Then
            With element ' com.sun.star.text.TextEmbeddedObject.Model
            If .supportsService("com.sun.star.formula.FormulaProperties") Then
                ' Force the update
                realElem = embeddedObjects.getByName(elementNames(i)).EmbeddedObject
                realElem.updateMode = com.sun.star.embed.EmbedUpdateModes.ALWAYS_UPDATE
                
                .BaseFontHeight = 8
                
                .FontNameVariables = "Times New Roman" 'change la police des variables
                .FontVariablesIsItalic = True 'précise si les variables sont en italique : True/False
                .FontVariablesIsBold = False  'précise si les variables sont en gras : True/False
                
                .FontNameFunctions = "Times New Roman" 'change la police des Fonctions
                .FontFunctionsIsItalic = False 'précise si les fonctions sont en italique : True/False
                .FontFunctionsIsBold = False 'précise si les fonctions sont en gras : True/False
                
                .FontNameNumbers = "Times New Roman" 'change la police des nombres
                .FontNumbersIsItalic = False 'précise si les nombres sont en italique : True/False
                .FontNumbersIsBold = False 'précise si les nombres sont en gras : True/False
                
                .FontNameText = "Times New Roman" 'change la police des textes
                .FontTextIsItalic = False 'précise si les textes sont en italique : True/False
                .FontTextIsBold = False 'précise si les textes sont en gras : True/False
                
                
            End If
            End With ' element As com.sun.star.formula.FormulaProperties
       End If

     Next i
 
    ThisComponent.reformat()

End Sub ' ChangeFormulasSizeAndFont